#Область ПрограммныйИнтерфейс

Процедура ЗарегистрироватьЗаписьСсылочногоОбъекта(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		РегистрыСведений.ЛК_ОчередьОбменаСЛКСсылочнымиДанными.ЗарегистрироватьДобавлениеОбъекта(Объект.Ссылка);	
	Иначе
		РегистрыСведений.ЛК_ОчередьОбменаСЛКСсылочнымиДанными.ЗарегистрироватьИзменениеОбъекта(Объект.Ссылка);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗарегистрироватьЗаписьРегистраСведений(НаборЗаписей) Экспорт  
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПользователейЛК = ЛК_ОбменДаннымиПовтИсп.МассивПользователейЛК();
	
	ТЗ = НаборЗаписей.ВыгрузитьКолонки();
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		Если МассивПользователейЛК.Найти(СтрокаНабора.ФизическоеЛицо) = Неопределено  Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНабора);
	КонецЦикла;	
	
	ТипОбъекта	= ЛК_ОбменДаннымиПовтИсп.ТипыОбъектов().РСНЗ;
	ИмяОбъекта = НаборЗаписей.Метаданные().Имя;
	Объект1С = ЛК_РаботаСJSON.ЗаписьJSON(ТЗ);
	
	Структура = Новый Структура;
	Структура.Вставить("ТипОбъекта", ТипОбъекта);
	Структура.Вставить("ИмяОбъекта", ИмяОбъекта);
	Структура.Вставить("Объект",     Объект1С);
				
	Попытка
		ДанныеJSON = ЛК_РаботаСJSON.ЗаписатьJSON_АП(Структура);		
	Исключение
		Объект = СтрШаблон(	"{name: ""%1"", object: %2}", 
							ИмяОбъекта, 
							Объект1С);
							
		ЛК_Логирование.ДобавитьЗаписьВЛог(	ЛК_ОбменДаннымиПовтИсп.УровниЛога().Ошибка,
											ЛК_ОбменДаннымиПовтИсп.СобытияЛога().РегистрацияЗаданияОчередиОбмена, 
											ЛК_ОбменДаннымиПовтИсп.УровниСобытийЛога().Ошибка,
											ЛК_Логирование.ДанныеJSONОбъектJSON(ЛК_Логирование.ТекущаяДатаЛК(),
																				,
																				ТипОбъекта,
																				ИмяОбъекта,
																				Объект, 
																				"РегистрыСведений.ЗарегистрироватьЗаписьРегистраСведений.ЗаписатьJSON_АП",
																				ОписаниеОшибки()));											
			
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецПопытки;
	
	ХешированиеДанных  = Новый ХешированиеДанных (ХешФункция.CRC32);
 	ХешированиеДанных.Добавить(ДанныеJSON);
 	ХешСумма = ХешированиеДанных.ХешСумма;   
	
	Если НаборЗаписей.Количество() = 0 Тогда
		РегистрыСведений.ЛК_ОчередьОбменаСЛКПроизвольнымиДанными.ЗарегистрироватьУдалениеОбъекта(ТипОбъекта, ИмяОбъекта, ХешСумма, ДанныеJSON);
	Иначе 	
		РегистрыСведений.ЛК_ОчередьОбменаСЛКПроизвольнымиДанными.ЗарегистрироватьДобавлениеОбъекта(ТипОбъекта, ИмяОбъекта, ХешСумма, ДанныеJSON);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры	

Процедура ЗарегистрироватьЗаписьРегистраНакоплений(НаборЗаписей) Экспорт  
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПользователейЛК = ЛК_ОбменДаннымиПовтИсп.МассивПользователейЛК();
	
	ИмяМетаданных = НаборЗаписей.Метаданные().Имя;
	ТипОбъекта = ЛК_ОбменДаннымиПовтИсп.ТипыОбъектов().РННЗ;
	
	Если ИмяМетаданных = "НачисленияУдержанияПоСотрудникам"  Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	НачисленияУдержанияПоСотрудникам.Сотрудник КАК Сотрудник,
		               |	НачисленияУдержанияПоСотрудникам.ПериодДействия КАК ПериодДействия,
		               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация
		               |ИЗ
		               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		               |ГДЕ
		               |	НачисленияУдержанияПоСотрудникам.Регистратор = &Регистратор";
		
		Запрос.УстановитьПараметр("Регистратор", НаборЗаписей.Отбор.Регистратор.Значение);
		
		Попытка
			ТЗ = Запрос.Выполнить().Выгрузить();	
		Исключение
			Объект = СтрШаблон(	"{name: ""%1""}", ИмяМетаданных);
								
			ЛК_Логирование.ДобавитьЗаписьВЛог(	ЛК_ОбменДаннымиПовтИсп.УровниЛога().Ошибка,
												ЛК_ОбменДаннымиПовтИсп.СобытияЛога().РегистрацияЗаданияОчередиОбмена, 
												ЛК_ОбменДаннымиПовтИсп.УровниСобытийЛога().Ошибка,				
												ЛК_Логирование.ДанныеJSONОбъектСтрока(ЛК_Логирование.ТекущаяДатаЛК(),
																					,
																					ТипОбъекта, 
																					ИмяМетаданных, 
																					"РегистрыСведений.ЗарегистрироватьЗаписьРегистраНакоплений.ВыгрузкаДанныхЗапроса",
																					ОписаниеОшибки()));			
			УстановитьПривилегированныйРежим(Ложь);
			Возврат;
		КонецПопытки;
				
		Для Каждого Запись Из НаборЗаписей Цикл
			Если МассивПользователейЛК.Найти(Запись.ФизическоеЛицо) = Неопределено  Тогда
				Продолжить;
			КонецЕсли;	
			
			НоваяСтрокаТЗ = ТЗ.Добавить();
			НоваяСтрокаТЗ.Сотрудник      = Запись.Сотрудник;
			НоваяСтрокаТЗ.ПериодДействия = Запись.ПериодДействия;
			НоваяСтрокаТЗ.ФизическоеЛицо = Запись.ФизическоеЛицо;
			НоваяСтрокаТЗ.Организация    = Запись.Организация;
		КонецЦикла;
		
		ТЗ.Свернуть("Сотрудник, ФизическоеЛицо, Организация, ПериодДействия");
		
		Для Каждого СтрокаТЗ Из Тз  Цикл
			Если МассивПользователейЛК.Найти(СтрокаТЗ.ФизическоеЛицо) = Неопределено  Тогда
				Продолжить;
			КонецЕсли;	

			ТипОбъекта	= ЛК_ОбменДаннымиПовтИсп.ТипыОбъектов().РасчетныйЛист;
			
			Структура = Новый Структура;
			Структура.Вставить("Сотрудник",      СтрокаТЗ.Сотрудник);
			Структура.Вставить("ФизическоеЛицо", СтрокаТЗ.ФизическоеЛицо);
			Структура.Вставить("Организация",    СтрокаТЗ.Организация);
			Структура.Вставить("Месяц",          СтрокаТЗ.ПериодДействия);

			ДанныеJSON = ЛК_РаботаСJSON.ЗаписьJSON(Структура);	
			
			ХешированиеДанных  = Новый ХешированиеДанных (ХешФункция.CRC32);
			ХешированиеДанных.Добавить(ДанныеJSON);
			ХешСумма = ХешированиеДанных.ХешСумма;
			
			РегистрыСведений.ЛК_ОчередьОбменаСЛКПроизвольнымиДанными.ЗарегистрироватьДобавлениеОбъекта(ТипОбъекта, ИмяМетаданных, ХешСумма, ДанныеJSON);
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры	

#КонецОбласти